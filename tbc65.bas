#RetroDevStudio.MetaData.BASIC:8193,BASIC 65,lowercase,10,10
10 GOTO 180
20 REM ------------------------------------
30 REM 2024-02-03
40 REM ------------------------------------
50 PRINT "SAVING TO DISK"
60 SAVE "@0:TBC3B",8
70 PRINT "DONE":DIR "TBC3B"
80 END
90 :
100 REM ------------------------------------
110 REM TBC IS BASED ON MICROCOMPILER
120 REM GERMAN RUN 86/7,PG 83  UPDATE 19.7.1987
130 REM MICRO-COMPILER BY VIC CORTES - RUN AUGUST 1985,PG 62
140 REM UPDATED AND COMMENTED FOR MEGA65
150 REM --------------------------------------------
160 REM
170 :
180 REM ------------------------------------
190 REM"  *** {SHIFT-I}{SHIFT-N}{SHIFT-I}{SHIFT-T} ***"
200 REM ------------------------------------
210 :
220 DIM SC(200)   :REM STRING KONSTANTE
230 DIM L(800)    :REM RELEVANTE ZEILENNUMMERN; JETZT ALLE ZEILENNUMMERN
240 DIM LA(800)   :REM UND IHRE ADDRESSEN
250 DIM LP(10),LF(10):REM FOR..NEXT SCHLEIFEN ADRESSEN
260 DIM XA(10)    :REM FOR..NEXT ADRESSEN
270 DIM V$(200)   :REM VARIABLE NAMES
280 DIM VA(200)   :REM VARIABLEN ADRESSEN
290 DIM P828(200) :REM READ BUFFER, C64: AB 828, C128: AB ADR 2816
300 :
310 L=0:REM  POINTER TO LL() LINE NUMBER
320 LP=0:REM LOOP PTR, INDEX FUER FOR-NEXT
330 XA=0:REM
340 VP=0:REM MAX. INDEX VARIABLE POINTER
350 VO=0:REM VARIABLE OFFSET; STARTADRESSE VAR TABLE
360 VE=0:REM VARIABLE AREA END. BEGINNING OF CONSTANT STRINGS (E.G. FILENAMES)
370 SC=0:REM MAX ENTRY IN SC()
380 :
390 A=0:REM
400 B=0:REM GELESENES BYTE
410 U=0:REM INDEX TO P828()
420 I=0:REM
430 J=0:REM
440 K=0:REM
450 V=0:REM
460 D=0:REM
470 C=0:REM
480 H=0:REM
490 Q=0:REM  QUOTE FLAG (INSIDE STRINGS)
500 W=0:REM
510 Z=0: REM ASCFLAG
520 :
530 EC=0:REM ERROR COUNTER
540 :
550 C$=CHR$(34):Z$=CHR$(0):CR$=CHR$(13)
560 OP=0: PC=$2200:REM PROGRAM COUNTER
570 :
580 T1%=0:REM TI STARTWERT
590 :
600 REM FOREGROUND 15:BACKGROUND 0
610 :
640 REM
650 REM --------------------------------------------
660 PRINT CHR$(147);CHR$(17);CHR$(5);CHR$(14);CHR$(8);"TINY-BASIC-COMPILER"
670 REM --------------------------------------------
680 REM
690 PA=0
700 :
710 S$="*"
720 PRINT:INPUT "QUELLFILE (*=ENDE) ";S$
730 IF S$="*" OR S$="" THEN END
740 :
750 T1%=TI:REM ZEIT MERKEN
760 PA=1:GOSUB 1350:REM PASS1
770 REM WENN FEHLER, DANN STOP
780 IF EC>0 THEN GOTO 800
790 PA=2:GOSUB 1350:REM PASS2
800 END
810 :
820 REM
830 REM --------------------------------------------
840 REM PRINT LINE TO BINARY
850 REM --------------------------------------------
860 REM
870 :
880 IF LEN(A$)=0 THEN RETURN
890 IF PA=1 THEN GOTO 930
900 FOR A=1 TO LEN(A$)
910 PRINT#3,MID$(A$,A,1);
920 NEXT A
930 PC=PC+LEN(A$)
940 RETURN
950 :
960 REM -----------------------------------------------
970 REM OPEN FILE A1$ FOR INPUT AS #2
980 REM -----------------------------------------------
990 :
1000 A1$=A1$+",P,R"
1010 DOPEN#2,(A1$)
1020 REM PRINT "FILE OPEN #2:";A1$
1030 IF DS THEN GOTO 1070
1040 RETURN
1050 :
1060 REM ERROR READING FILE
1070 PRINT "ERROR READING "+A1$
1080 DCLOSE#2:REM PRINT "DCLOSE#2"
1090 END
1100 :
1110 REM
1120 REM -----------------------------------------------
1130 REM OPEN FILE "P-".A1$.",P,W" FOR OUTPUT AS #3
1140 REM -----------------------------------------------
1150 REM
1160 IF PA<2 THEN GOTO 1290
1170 A1$="R-"+A1$+""
1180 REM DOPEN#3,("@"+A1$),W
1190 DOPEN#3,("@"+A1$+",P"),W
1200 IF DS THEN 1340
1210 RESTORE:REM BASIC STUB UND RTL SCHREIBEN
1220 READ A$:IF LEFT$(A$,1) ="*" THEN GOTO 1290
1230 H=ASC(MID$(A$,1,1))-48:L=ASC(MID$(A$,2,1))-48
1240 IF H>16 THEN H=H-7
1250 IF L>16 THEN L=L-7
1260 A=H*16+L
1270 PRINT#3,CHR$(A);
1280 GOTO 1220
1290 RETURN
1300 :
1310 PRINT "ERROR WRITING FILE "+A1$
1320 DCLOSE#3:REM PRINT "DCLOSE#3"
1330 END
1340 :
1350 REM ----------------------------------------
1360 REM"  {SHIFT-S}OURCE LESEN"
1370 REM ----------------------------------------
1380 :
1390 PRINT "PASS ";PA
1400 EC=0:LL=0:LP=0:SC=0:PC=$2200
1410 VE=VO+2*VP+1:REM VARIABLE ENDE
1420 A1$=S$:GOSUB 960: REM OPEN READ
1430 DCLOSE#2
1440 A1$=S$:GOSUB 960: REM OPEN READ
1450 :
1460 A1$=S$:GOSUB 1110: REM OPEN WRITE
1470 :
1480 REM A$=" ; PROGRAM = "+S$
1490 REM A$=" ;":A$=" ; *="+S$
1500 REM PRINT "FILE#3 OPENED"
1510 GET#2,A1$,A2$:REM  TI$="000000"
1520 GET#2,A1$,A2$:T=ASC(A1$+Z$)+256*ASC(A2$+Z$)
1530 REM PRINT "LINEPTR = ";T
1540 IF T=0 THEN GOTO 4130:REM PTR TO NEXT LINE IS ZERO? -> JUMP CODE ENDE
1550 GET#2,A1$,A2$:REM READ LINE NUMBER
1560 T=ASC(A1$+Z$)+ASC(A2$+Z$)*256
1570 PRINT T;" ": PRINT"{UP}";
1580 Q=0:REM UNSET QUOTE FLAG
1590 REM A$=" ; ZEILE"+STR$(T)
1600 L(LL)=T: LA(LL)=PC
1610 LL=LL+1
1620 REM
1630 J=0:REM  EINE ZEILE LESEN
1640 REM READ SYMBOL AND STORE INTO BUFFER
1650 GET#2,B$:IF ST THEN GOTO 4130
1660 B=ASC(B$+Z$):P828(J)=B
1670 IF Q OR (B<>32 AND B>0) THEN J=J+1
1680 IF B=0 THEN GOTO 1710
1690 IF B=34 THEN Q=NOT Q
1700 IF Q THEN GOTO 1650
1710 IF B<32 THEN GOSUB 1750:GOTO 1520
1720 IF B=$A7 THEN GOSUB 1750:GOTO 1630:REM 'THEN
1730 GOTO 1650
1740 :
1750 REM ----------------------------------------
1760 REM  EVALUATE COMMAND
1770 REM ----------------------------------------
1780 :
1790 B=P828(0  ):U=1  :P828(J)=0:P828(J+1)=0
1800 IF B=136 THEN GOTO 3890:REM LET
1810 REM IF B=128 OR B=$90 THEN PRINT#3,"JMP $A474":RETURN:REM C64 END STOP
1820 REM CHECK: LDA #$80:JSR TOKEN-EVAL-ROUTINE (C64 $A7E7)
1830 IF B=$80 OR B=$90 THEN GOTO 6680:REM 'END STOP
1840 IF B=142 THEN A$=CHR$($60):GOSUB 820:RETURN:REM 'RETURN
1850 IF B=158 THEN GOTO 6350:REM 'SYS
1860 IF B=159 THEN GOTO 6770:REM 'OPEN
1870 IF B=139 THEN GOTO 4960:REM 'IF
1880 IF B=160 THEN GOTO 7100:REM 'CLOSE
1890 IF B=153 THEN GOTO 5310:REM 'PRINT
1900 IF B=152 THEN GOTO 7360:REM 'PRINT#
1910 IF B=151 THEN GOTO 6440:REM 'POKE
1920 IF B=161 THEN GOTO 7190:REM 'GET
1930 IF B=129 THEN GOTO 5880:REM 'FOR
1940 IF B=130 THEN GOTO 6240:REM 'NEXT
1950 IF B=143 AND P828(U)=36 THEN GOTO 4730:REM 'REM$
1960 IF B=143 THEN RETURN:REM 'REM
1970 IF B=137 THEN A$=CHR$($4C):GOTO 5760:REM 'GOTO
1980 IF B=141 THEN A$=CHR$($20):GOTO 5760:REM 'GOSUB
1990 IF B=254 AND P828(U)=2 THEN GOTO 6590:REM 'BANK
2000 IF B<48 OR B>90 THEN E$="NUM OR CHAR EXPECTED":GOTO 2050
2010 IF B>64 THEN GOTO 3940
2020 IF B<58 THEN U=0:B=$A9:GOTO 1970:REM 'GOTO
2030 E$="CMD NOT FOUND"
2040 :
2050 REM ----------------------------------------
2060 REM SHOW ERROR
2070 REM ----------------------------------------
2080 :
2090 PRINT"{DOWN}{RVSON}{SHIFT-E}{2SHIFT-R}{SHIFT-O}{SHIFT-R}: ";E$;": LINE";L(LL-1);" POS ";U;
2100 PRINT " TOKEN ";P828(U);CHR$(157);"/$";RIGHT$(HEX$(P828(U)),2)
2110 EC=EC+1:E$=""
2120 RETURN
2130 :
2140 REM -----------------------------------
2150 REM"  {SHIFT-E}RSTE VARIABLE"
2160 REM -----------------------------------
2170 :
2180 GOSUB 3110
2190 REM PRINT"1032: V=";V;"N=";N
2200 REM " LDA #<"+STR$(N)+": LDX #>"+STR$(N)
2210 IF V<>0 THEN GOTO 2260
2220 D=N:GOSUB 3810
2230 A$=CHR$($A9)+CHR$(L)+CHR$($A2)+CHR$(H):GOSUB 820
2240 GOTO 2300
2250 REM " LDA I"+V$+": LDX I"+V$+"+1"
2260 REM PRINT"1045: V$=";V$;" D=";HEX$(D);" VL=";HEX$(VL);" VH=";HEX$(VH)
2270 A$=CHR$($AD)+CHR$(VL)+CHR$(VH):GOSUB 820
2280 D=D+1:GOSUB 3810
2290 A$=CHR$($AE)+CHR$(L)+CHR$(H):GOSUB 820
2300 RETURN
2310 :
2320 REM -----------------------------------
2330 REM"  {SHIFT-A}USDRUCK"
2340 REM -----------------------------------
2350 :
2360 P=0
2370 IF P828(U)=$C2 THEN U=U+2:P=1  :REM PEEK
2380 GOSUB 2180
2390 IF U>100 THEN PRINT"{RVSON}LINE OVERFLOW":EC=EC+1:RETURN
2400 O=0:B=P828(U)
2410 IF B=$AD THEN GOTO 2830:REM '/
2420 IF B=$AC THEN GOTO 2830:REM '*
2430 REM " CLC"
2440 IF B=$AA THEN O=109:A$=CHR$($18):GOSUB 820:REM '+
2450 REM " SEC"
2460 IF B=$AB THEN O=237:A$=CHR$($38):GOSUB 820:REM '-
2470 IF B=$AF THEN O=45                        :REM 'AND
2480 IF B=$B0 THEN O=13                        :REM 'OR
2490 IF O=0 THEN GOTO 2970:REM PEEK
2500 U=U+1:GOSUB 3110
2510 IF V=0 THEN GOTO 2670:REM ZAHL? DANN GOTO
2520 REM VARIABLE
2530 A$=CHR$(VL)+CHR$(VH):DD=D:REM DEZIMAL MERKEN
2540 IF O=109 THEN A$=CHR$($6D)+A$:GOSUB 820:REM ADC LOW
2550 IF O=237 THEN A$=CHR$($ED)+A$:GOSUB 820:REM SBC
2560 IF O=45  THEN A$=CHR$($2D)+A$:GOSUB 820:REM AND
2570 IF O=13  THEN A$=CHR$($0D)+A$:GOSUB 820:REM ORA
2580 A$=CHR$($A8)+CHR$($8A):GOSUB 820
2590 D=D+1:GOSUB 3810:A$=CHR$(L)+CHR$(H)
2600 IF O=109 THEN A$=CHR$($6D)+A$:GOSUB 820:REM ADC HI
2610 IF O=237 THEN A$=CHR$($ED)+A$:GOSUB 820:REM SBC
2620 IF O=45  THEN A$=CHR$($2D)+A$:GOSUB 820:REM AND
2630 IF O=13  THEN A$=CHR$($0D)+A$:GOSUB 820:REM ORA
2640 GOTO 2790
2650 REM
2660 REM NUM. OPERAND
2670 D=N:GOSUB 3810:A$=CHR$(L)
2680 IF O=109 THEN A$=CHR$($69)+A$:GOSUB 820:REM ADC #LOW
2690 IF O=237 THEN A$=CHR$($E9)+A$:GOSUB 820:REM SBC #
2700 IF O=45  THEN A$=CHR$($29)+A$:GOSUB 820:REM AND #
2710 IF O=13  THEN A$=CHR$($09)+A$:GOSUB 820:REM ORA #
2720 REM  TAY: TXA
2730 A$=CHR$($A8)+CHR$($8A):GOSUB 820
2740 A$=CHR$(H)
2750 IF O=109 THEN A$=CHR$($69)+A$:GOSUB 820:REM ADC #HI
2760 IF O=237 THEN A$=CHR$($E9)+A$:GOSUB 820:REM SBC #
2770 IF O=45  THEN A$=CHR$($29)+A$:GOSUB 820:REM AND #
2780 IF O=13  THEN A$=CHR$($09)+A$:GOSUB 820:REM ORA #
2790 REM  TAX: TYA
2800 A$=CHR$($AA)+CHR$($98):GOSUB 820
2810 GOTO 2390
2820 :
2830 REM ------------------------------
2840 REM"  {SHIFT-M}UL/{SHIFT-D}IV"
2850 REM ------------------------------
2860 :
2870 A$=CHR$($85)+CHR$(97)+CHR$($86)+CHR$(98):GOSUB 820
2880 U=U+1:GOSUB 2180
2890 G=G OR 1
2900 A=24
2910 IF B=173 THEN A=56:A$=CHR$($38):GOSUB 820:REM /
2920 IF A=24 THEN A$=CHR$($18):GOSUB 820
2930 REM A$=" JSR MULDIV"
2940 A$=CHR$($20)+CHR$($93)+CHR$($20):GOSUB 820
2950 GOTO 2390
2960 :
2970 REM ------------------------------
2980 REM"  {SHIFT-P}{2SHIFT-E}{SHIFT-K}(AUSDR)"
2990 REM ------------------------------
3000 :
3010 IF P=0 THEN RETURN
3020 G = G OR 8
3030 REM A$=" STA 34: STX 35"
3040 A$=CHR$($85)+CHR$(34)+CHR$($86)+CHR$(35):GOSUB 820
3050 REM A$=" LDA #34: LDY #0"
3060 A$=CHR$($A9)+CHR$(34)+CHR$($A0)+CHR$(0):GOSUB 820
3070 REM A$=" JSR PEEK"
3080 A$=CHR$($20)+CHR$($05)+CHR$($21):GOSUB 820
3090 U=U+1:P=0:GOTO 2390
3100 :
3110 REM ------------------------------
3120 REM"  {SHIFT-F}IND"
3130 REM RETURN: V=0, DANN NUMERISCH IN N
3140 REM         V>0, DANN VARIABLE IN V$
3150 REM ------------------------------
3160 :
3170 N=0:V=0
3180 IF P828(U)<65 THEN GOTO 3200
3190 IF P828(U)<91 THEN GOTO 3530
3200 T=0
3210 IF P828(U)=170 THEN U=U+1:GOTO 3230
3220 IF P828(U)=171 THEN U=U+1:T=1
3230 IF P828(U)<48 OR P828(U)>57 THEN E$="NUM EXPECTED":GOSUB 2050
3240 IF P828(U)>47 AND P828(U)<58 THEN N=N*10+P828(U)-48:U=U+1:GOTO 3240
3250 IF T=0 THEN GOTO 3270
3260 N=65536-N
3270 RETURN
3280 :
3290 REM ----------------------------------------
3300 REM"  {SHIFT-V}ARIABLE SUCHEN"
3310 REM ----------------------------------------
3320 REM INPUT: V$; OUTPUT VV
3330 :
3340 VV=0
3350 IF V$(VV) = V$ THEN GOTO 3390
3360 VV=VV+1
3370 IF VV>=VP THEN VV=-1:RETURN:REM NOT FOUND
3380 GOTO 3350
3390 D=VA(VV):GOSUB 3810
3400 VH=H:VL=L
3410 RETURN
3420 REM ----------------------------------------
3430 REM" {SHIFT-Z}EILENNUMMER NACH {SHIFT-A}DRESSE WANDELN"
3440 REM ----------------------------------------
3450 REM IN: N ; OUT: H/L
3460 D=-1:T9=0:H=0:L=0
3470 IF L(T9)=N THEN D=LA(T9):GOTO 3500
3480 T9=T9+1:IF T9<LL THEN GOTO 3470
3490 GOTO 3510
3500 GOSUB 3810
3510 RETURN
3520 :
3530 REM ------------------------------
3540 REM"  {SHIFT-V}{SHIFT-A}{SHIFT-R}{SHIFT-I}{SHIFT-A}{SHIFT-B}{SHIFT-L}{SHIFT-E}"
3550 REM VV=INDEX VAR, VH/VL = ADRESSE
3560 REM ------------------------------
3570 :
3580 V=P828(U):V$=CHR$(V)
3590 V=P828(U+1)
3600 IF (V>64 AND V<91)OR(V>47 AND V<58) THEN V$=V$+CHR$(V):U=U+1:GOTO 3590
3610 U=U+1:T=P828(U)
3620 IF T>90 THEN GOTO 3660:REM IF >'Z' ?
3630 IF T<32 THEN GOTO 3660
3640 IF (T=59)OR(T=44)OR(T=41) THEN GOTO 3660:REM ; , )
3650 IF T>35 THEN GOTO 3610:REM IF ># ?
3660 V$=LEFT$(V$,6)
3670 IF VP=0 THEN GOTO 3770:REM FIRST VAR AT ALL? -->
3680 I=0:VV=-1:REM VV=INDEX AUF GEFUNDENE VARIABLE, -1 = NOT FOUND
3690 IF V$<>V$(I) THEN GOTO 3730
3700 VV=I:REM VAR FOUND, INDEX MERKEN
3710 D=VV*2+VO:GOSUB 3810:VH=H:VL=L
3720 GOTO 3750
3730 I=I+1: IF I<VP THEN GOTO 3690
3740 :
3750 V=1: REM FLAG: VARIABLE GELESEN
3760 IF VV>=0 THEN RETURN
3770 V$(VP)=V$:D=VO+2*VP:VA(VP)=D
3780 VP=VP+1
3790 REM PRINT "VAR ";V$;", VP=";VP
3800 :
3810 REM ------------------------------
3820 REM"  {SHIFT-H}/{SHIFT-L}"
3830 REM ------------------------------
3840 :
3850 IF D<0 THEN D=65536+D
3860 H%=D/256:L=D-H%*256:H=H%
3870 RETURN
3880 :
3890 REM ----------------------------------------
3900 REM"  {SHIFT-V}={SHIFT-A}USDR."
3910 REM ----------------------------------------
3920 :
3930 FOR I=0 TO 140:P828(I)=P828(I+1):NEXT
3940 U=0
3950 IF P828(U)<65 OR P828(U)>90 THEN E$="CHAR EXPECTED":GOTO 2050
3960 REM PRINT "{SHIFT-V}AR={SHIFT-E}XPR"
3970 V=P828(U)
3980 V$=CHR$(V):U=U+1
3990 V=P828(U)
4000 IF(V>64 AND V<91)OR(V>47 AND V<58) THEN V$=V$+CHR$(V):U=U+1:GOTO 3990
4010 IF P828(U)<>178 THEN E$="= EXPECTED":GOTO 2050:REM '=
4020 V1$=V$:U=U+1:GOSUB 2320:REM AUSDRUCK AUSWERTEN
4030 V$=V1$:GOSUB 3660:REM VARIABLE AUSWERTEN
4040 REM D1=D
4050 A$=CHR$($8D)+CHR$(L)+CHR$(H):GOSUB 820:REM " STA I"+V$
4060 D=D+1:GOSUB 3810
4070 A$=CHR$($8E)+CHR$(L)+CHR$(H):GOSUB 820:REM " STX I"+V$+"+1"
4080 RETURN
4090 :
4100 REM --------------------------------------------
4110 REM"  {SHIFT-E}NDE"
4120 REM --------------------------------------------
4130 REM PRINT "{SHIFT-E}ND OF INPUT FILE"
4140 L(LL)=65535
4150 A$=CHR$($60):GOSUB 820
4160 VO=PC:REM START DER TABELLE MERKEN FUER PASS2
4170 REM A$="ENDSTOP RTS":REM C64= JMP $A474, M65=?
4180 REM PRINT "Z=";Z:IF Z>127 THEN GOTO LABEL6550: REM FLAG: NO VARS
4190 IF VP=0 THEN GOTO 4270: REM JUMP, IF NO VAR READ
4200 IF PA>1 THEN PRINT"VARIABLE ($";HEX$(VO);")"
4210 A$=CHR$(0)+CHR$(0):REM 2 BYTE JE VARIABLE
4220 FOR I=0 TO VP-1
4230 IF PA=1 THEN VA(I)=PC:REM ADRESSE DER VARIABLEN SPEICHERN
4240 GOSUB 820:REM "I"+V$(I)+" .WORD 0"
4250 NEXT I
4260 :
4270 REM A$=" .END"
4280 IF PA=2 THEN PRINT#3,CHR$(0);
4290 DCLOSE#2:DCLOSE#3:REM PRINT "CLOSE 2:CLOSE 3"
4300 :
4310 PRINT"{DOWN}ERRORS";EC
4320 T1%=TI-T1%
4330 IF PA=2 THEN PRINT S$;" COMPILIERT";", ZEIT:";T1%;"S"
4340 RETURN:REM END
4350 REM --------------------------------------------
4360 :
4370 :
4380 REM" {SHIFT-T}{SHIFT-B}{SHIFT-C}-{SHIFT-L}IB"
4390 DATA  01,20,1E,20,E7,07,DE,9C, 3A,FE,02,30,3A,9E,38,32:REM 0000
4400 DATA  32,34,3A,8F,54,42,43,20, 32,33,31,32,32,33,00,00:REM 0010
4410 DATA  00,4C,00,22,85,16,86,17, 24,17,10,1B,48,A9,2D,20:REM 0020
4420 DATA  D2,FF,38,A9,00,E5,16,85, 16,A9,00,E5,17,85,17,68
4430 DATA  4C,46,20,85,16,86,17,A9, 00,8D,88,20,A0,08,A2,FF
4440 DATA  38,A5,16,F9,89,20,85,16, A5,17,F9,8A,20,85,17,E8
4450 DATA  B0,EF,A5,16,79,89,20,85, 16,A5,17,79,8A,20,85,17
4460 DATA  8A,D0,07,AD,88,20,D0,09, F0,0A,A2,30,8E,88,20,09
4470 DATA  30,20,D2,FF,88,88,10,C6, 60,00,01,00,0A,00,64,00
4480 DATA  E8,03,10,27,85,63,86,64, A2,00,86,65,86,66,A0,10
4490 DATA  90,22,06,61,26,62,26,65, 26,66,38,A5,65,E5,63,AA
4500 DATA  A5,66,E5,64,90,06,86,65, 85,66,E6,61,88,D0,E3,A5
4510 DATA  61,A6,62,60,46,66,66,65, 66,62,66,61,88,30,F0,90
4520 DATA  F3,18,A5,65,65,63,85,65, A5,66,65,64,85,66,18,90
4530 DATA  E3,85,04,86,05,A5,1A,85, 02,AD,04,11,48,A9,01,1C
4540 DATA  04,11,20,6E,FF,68,8D,04, 11,58,60,08,DB,AB,D1,02:REM 00F0
4550 DATA  20,77,FF,FB,28,60,08,DB, DA,AA,AB,D1,02,B5,01,C9:REM 0100
4560 DATA  20,B0,02,A3,00,20,74,FF, FA,FB,28,29,FF,60,55,55:REM 0110
4570 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 0120
4580 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 0130
4590 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 0140
4600 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 0150
4610 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 0160
4620 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 0170
4630 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 0180
4640 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 0190
4650 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 01A0
4660 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 01B0
4670 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 01C0
4680 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 01D0
4690 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 01E0
4700 DATA  55,55,55,55,55,55,55,55, 55,55,55,55,55,55,55,55:REM 01F0
4710 DATA  55,*                                            :REM 0200
4720 :
4730 REM ------------------------------
4740 REM"  {SHIFT-R}{SHIFT-E}{SHIFT-M}$" - COMPILER FLAGS
4750 REM ------------------------------
4760 :
4770 U=U+2:W=P828(U-1)
4780 IF W<>81 THEN GOTO 4860:REM Q (QUIT COMPILER IF ERROR COUNT >0)
4790 IF EC=0 THEN RETURN
4800 :
4810 DCLOSE#3:DCLOSE#2:REM PRINT "CLOSE 2:CLOSE 3"
4820 :
4830 PRINT:PRINT"***{SHIFT-A}BGEBROCHEN NACH";EC;"{SHIFT-F}EHLER(N)"
4840 END
4850 :
4860 IF W<>65 THEN GOTO 4920:REM A (INSERT ASSEMBLER COMMAND)
4870 U=U+1
4880 W=P828(U):U=U+1
4890 IF W>0 THEN A$=CHR$(W):GOSUB 820:GOTO4880
4900 A$="":GOSUB 820
4910 RETURN
4920 IF W<>76 THEN RETURN:REM L (INCLUDE MUL/DIV LIBRARY)
4930 Z=Z OR 64
4940 RETURN
4950 :
4960 REM ------------------------------
4970 REM"  {SHIFT-I}{SHIFT-F}/{SHIFT-T}{SHIFT-H}{SHIFT-E}{SHIFT-N}"
4980 REM ------------------------------
4990 :
5000 REM PRINT "{SHIFT-I}{SHIFT-F}/{SHIFT-T}{SHIFT-H}{SHIFT-E}{SHIFT-N}"
5010 GOSUB 2320:W=P828(U):IF W<$B1 THEN GOTO 2050:REM '>
5020 IF W>$B3 THEN E$="IF ILL CHAR "+STR$(W):GOTO 2050:REM '<
5030 REM AB HIER NUR '<, =, >
5040 REM A$=" STA 36: STX 37"
5050 A$=CHR$($85)+CHR$(36)+CHR$($86)+CHR$(37):GOSUB 820
5060 U=U+1
5070 IF W=$B3 AND P828(U)=$B1 THEN W=180:U=U+1:REM '<>
5080 IF W=$B1 AND P828(U)=$B3 THEN W=180:U=U+1:REM '><
5090 GOSUB 2320
5100 REM A$=" CPX 37: BEQ"
5110 A$=CHR$($E4)+CHR$(37)+CHR$($F0):GOSUB 820
5120 CM$=CHR$($C5)+CHR$(36)
5130 D=LA(LL):GOSUB 3810
5140 A$=""
5150 REM '<>
5160 IF W=180 THEN A$=CHR$(05)+CHR$($D0)+CHR$(07)
5170 IF W=180 THEN A$=A$+CHR$($4C)+CHR$(L)+CHR$(H)+CM$+CHR$($F0)+CHR$($F9)
5180 REM '=
5190 IF W=$B2 THEN A$=CHR$(03)+CHR$($4C)+CHR$(L)+CHR$(H)+CM$+CHR$($D0)+CHR$($F9)
5200 REM '<
5210 IF W=$B3 THEN A$=CHR$(07)+CHR$($90)+CHR$(2)+CHR$($B0)+CHR$(9)
5220 IF W=$B3 THEN A$=A$+CHR$($4C)+CHR$(L)+CHR$(H)
5230 IF W=$B3 THEN A$=A$+CM$+CHR$($90)+CHR$($F9)+CHR$($F0)+CHR$($F7)
5240 REM '>
5250 IF W=$B1 THEN A$=CHR$(07)+CHR$($B0)+CHR$(2)+CHR$($90)+CHR$(9)
5260 IF W=$B1 THEN A$=A$+CHR$($4C)+CHR$(L)+CHR$(H)
5270 IF W=$B1 THEN A$=A$+CM$+CHR$($B0)+CHR$($F9)+CHR$($F0)+CHR$($F7)
5280 GOSUB 820
5290 RETURN
5300 :
5310 REM ------------------------------
5320 REM"  {SHIFT-P}{SHIFT-R}{SHIFT-I}{SHIFT-N}{SHIFT-T}"
5330 REM ------------------------------
5340 :
5350 W=P828(U):IF W<32 THEN GOTO 5730
5360 IF W=59 AND P828(U+1)<32 THEN RETURN
5370 IF W=59 THEN U=U+1:GOTO 5310
5380 IF W=199 THEN GOTO 5520:REM" {SHIFT-C}{SHIFT-H}{SHIFT-R}$
5390 IF W=34  THEN GOTO 5580:REM" {SHIFT-S}TRING
5400 REM ------------------------------
5410 REM"  {SHIFT-P}{SHIFT-R}{SHIFT-I}{SHIFT-N}{SHIFT-T} {SHIFT-A}USDR."
5420 REM PRINT#3," LDA #29"
5430 REM PRINT#3," JSR $FFD2"
5440 GOSUB 2320:REM A$=" STX 34: TAX: LDA 34":REM A/X TAUSCHEN
5450 G=G OR 4
5460 A$=CHR$($20)+CHR$($23)+CHR$($20):GOSUB 820:REM" JSR PRTSGN" - ZAHL IN A/X AUSGEBEN
5470 REM C64 $BDCD, C128 $8E32, C65 $647F, M65 $????
5480 A$=CHR$($A9)+" "+CHR$($20)+CHR$($D2)+CHR$($FF):GOSUB 820:REM " LDA #32: JSR $FFD2"
5490 GOTO 5310
5500 REM ------------------------------
5510 REM"  {SHIFT-P}{SHIFT-R}{SHIFT-I}{SHIFT-N}{SHIFT-T} {SHIFT-C}{SHIFT-H}{SHIFT-R}$({SHIFT-A}USDR.)"
5520 U=U+1:IF P828(U)<>40 THEN GOTO 2050
5530 U=U+1:GOSUB 2320
5540 A$=CHR$($20)+CHR$($D2)+CHR$($FF):GOSUB 820:REM " JSR $FFD2"
5550 U=U+1:GOTO 5310
5560 REM ------------------------------
5570 REM"  {SHIFT-P}{SHIFT-R}{SHIFT-I}{SHIFT-N}{SHIFT-T} {SHIFT-S}TRING"
5580 A$=CHR$($20)+CHR$($7D)+CHR$($FF):GOSUB 820:REM " JSR PRIMM, $FF7D"
5590 G=G OR 2
5600 REM A$=" .TEXT '":GOSUB 440
5610 I=0
5620 I=I+1:U=U+1:IF U>100 THEN GOTO 5680
5630 IF I>20 THEN I=1:REM A$="'":A$=" .TEXT '"
5640 IF P828(U)=34 THEN GOTO 5680
5650 IF P828(U)=0 THEN GOTO 5680
5660 A$=CHR$(P828(U)):GOSUB 820
5670 GOTO 5620
5680 REM A$="'"
5690 A$=CHR$(0):GOSUB 820:REM " .BYTE 0"
5700 U=U+1:GOTO 5310
5710 REM
5720 REM ZEILE ABSCHLIESSEN
5730 A$=CHR$($A9)+CHR$(13)+CHR$($20)+CHR$($D2)+CHR$($FF):GOSUB 820:REM " LDA #13: JSR $FFD2"
5740 RETURN
5750 :
5760 REM ------------------------------------
5770 REM"  {SHIFT-G}{SHIFT-O}{SHIFT-T}{SHIFT-O}/{SHIFT-G}{SHIFT-O}{SHIFT-S}{SHIFT-U}{SHIFT-B}"
5780 REM ------------------------------------
5790 :
5800 GOSUB 3110
5810 REM IF V=0 THEN V$=MID$(STR$(N),2)
5820 IF V<>0 THEN E$="GOTO/GOSUB":GOTO 2050
5830 GOSUB 3420
5840 A$=A$+CHR$(L)+CHR$(H):GOSUB 820
5850 IF (PA=2) AND (D < 0) THEN E$="ILL TARGET":GOTO 2050
5860 RETURN
5870 :
5880 REM ------------------------------------
5890 REM"  {SHIFT-F}{SHIFT-O}{SHIFT-R}"
5900 REM ------------------------------------
5910 :
5920 U=3:GOSUB 2320
5930 REM
5940 REM A1$=MID$(STR$(LP(LP)),2)
5950 REM A$=" JMP LF"+A1$
5960 D=LF(LP):GOSUB 3810:A$=CHR$($4C)+CHR$(L)+CHR$(H):GOSUB 820
5970 HU=U:U=1
5980 LP(LP)=PC:REM A$="F"+A1$+" ":GOSUB 440
5990 GOSUB 2180:U=HU+1
6000 REM A$=" STA 36: STX 37"
6010 A$=CHR$($85)+CHR$(36)+CHR$($86)+CHR$(37)
6020 GOSUB 820
6030 GOSUB 2320
6040 REM CPX $25: BEQ 4: BCS 11
6050 A$=CHR$($E4)+CHR$($25)+CHR$($F0)+CHR$($04)+CHR$($B0)+CHR$($0B)
6060 GOSUB 820
6070 REM BCC 6
6080 A$=CHR$($90)+CHR$($06)
6090 GOSUB 820
6100 REM CMP $24: BEQ 2: BCS 3
6110 A$=CHR$($C5)+CHR$($24)+CHR$($F0)+CHR$($02)+CHR$($B0)+CHR$($03)
6120 GOSUB 820
6130 D=XA(LA):GOSUB 3810
6140 A$=CHR$($4C)+CHR$(L)+CHR$(H):GOSUB 820:REM" JMP FF"+A1$
6150 REM OHNE STEP BEFEHL, DANN PLUS 1 " LDA #1: LDX #0"
6160 IF P828(U)<>169 THEN A$=CHR$($A9)+CHR$(1)+CHR$($A2)+CHR$(0):GOSUB820:GOTO 6180:REM 'STEP
6170 U=U+1:GOSUB 2320
6180 U=0  :B=170:GOSUB 2430:REM '+'
6190 D=PC:GOSUB 3810:
6200 LF(LP)=PC:REM"LF"+A1$+" "
6210 LP=LP+1
6220 U=1  :GOSUB 3530:GOTO 4050
6230 :
6240 REM ------------------------------------
6250 REM"  {SHIFT-N}{SHIFT-E}{SHIFT-X}{SHIFT-T}"
6260 REM ------------------------------------
6270 :
6280 LP=LP-1
6290 D=LP(LP):GOSUB 3810
6300 A$=CHR$($4C)+CHR$(L)+CHR$(H):GOSUB 820:REM" JMP F"+A1$
6310 XA(LP)=PC
6320 A$=CHR$($EA):GOSUB 820:REM"FF"+A1$+" NOP"
6330 RETURN
6340 :
6350 REM ------------------------------------
6360 REM"  {SHIFT-S}{SHIFT-Y}{SHIFT-S}"
6370 REM ------------------------------------
6380 :
6390 GOSUB 2320
6400 A$=CHR$($20)+CHR$($E0)+CHR$($20):REM" JSR SYSCALL"
6410 GOSUB 820
6420 RETURN
6430 :
6440 REM ------------------------------------
6450 REM"  {SHIFT-P}{SHIFT-O}{SHIFT-K}{SHIFT-E}"
6460 REM ------------------------------------
6470 :
6480 GOSUB 2320
6490 A$=CHR$($85)+CHR$(36)+CHR$($86)+CHR$(37):REM" STA 36: STX 37"
6500 GOSUB 820
6510 REM
6520 IF P828(U)<>44 THEN GOTO 2050
6530 U=U+1:GOSUB 2320
6540 A$=CHR$($A0)+CHR$(0)+CHR$($A2)+CHR$(36):REM" LDY #0: LDX #36"
6550 GOSUB 820
6560 A$=CHR$($20)+CHR$($FA)+CHR$($20):REM" JSR POKE"
6570 RETURN
6580 :
6590 REM ------------------------------
6600 REM"  {SHIFT-B}{SHIFT-A}{SHIFT-N}{SHIFT-K}"
6610 REM ------------------------------
6620 :
6630 U=U+1:GOSUB 2320
6640 A$=CHR$($8D)+CHR$($D1)+CHR$(2):REM" STA $2D1"
6650 RETURN
6660 REM
6670 :
6680 REM ------------------------------------
6690 REM"  {SHIFT-E}{SHIFT-N}{SHIFT-D}, {SHIFT-S}{SHIFT-T}{SHIFT-O}{SHIFT-P}"
6700 REM ------------------------------------
6710 :
6720 IF PA<2 THEN PC=PC+3:GOTO 6750
6730 D=VO-1:GOSUB 3810
6740 A$=CHR$($4C)+CHR$(L)+CHR$(H):GOSUB 820
6750 RETURN
6760 :
6770 REM -----------------------------------------
6780 REM"  {SHIFT-O}{SHIFT-P}{SHIFT-E}{SHIFT-N}"
6790 REM -----------------------------------------
6800 :
6810 GOSUB 2320:A$=CHR$($48):GOSUB 820:REM " PHA" PARAMETER FN
6820 IF P828(U)<>44 THEN GOTO 6960
6830 U=U+1:GOSUB 2320:A$=CHR$($48):GOSUB 820:REM " PHA" PARAMETER GN
6840 IF P828(U)<>44 THEN GOTO 6960
6850 U=U+1:GOSUB 2320:A$=CHR$($48):GOSUB 820:REM " PHA" PARAMETER SA
6860 IF P828(U)<>44 THEN GOTO 6960
6870 L$="":U=U+1
6880 IF P828(U)<>34 THEN GOTO 6960
6890 REM FILENAME IN ""
6900 U=U+1:W=P828(U):IF W=0 OR W=34 THEN GOTO 6930
6910 L$=L$+CHR$(W)
6920 GOTO 6900
6930 SC$(SC)=L$:SC=SC+1
6940 GOTO 6990
6950 :
6960 E$="OPEN F,G,S,"+CHR$(34)+"FNAME"+CHR$(34)
6970 GOTO 2050
6980 :
6990 REM A$=" LDA #"+LEN(L$)+";OPEN"
7000 A$=CHR$($D0)+CHR$(LEN(L$)):REM BNE *+ LEN(L$)
7010 D=PC:GOSUB 3810:A$=L$:GOSUB 820
7020 A$=CHR$($A9)+CHR$(LEN(L$)):GOSUB 820:REM LDA #LEN
7030 A$=CHR$($A0)+CHR$(H)+CHR$($A2)+CHR$(L):GOSUB 820:REM  LDY #2:LDX #0
7040 A$=CHR$($20)+CHR$($BD)+CHR$($FF):GOSUB 820:REM  JSR $FFBD;SETNAM
7050 A$=CHR$($68)+CHR$($A8)+CHR$($68)+CHR$($AA)+CHR$($68):GOSUB 820:REM PLA:TAY:PLA:TAX:PLA
7060 A$=CHR$($20)+CHR$($BA)+CHR$($FF):GOSUB 820:REM " JSR $FFBA;SETLFS"
7070 A$=CHR$($20)+CHR$($C0)+CHR$($FF):GOSUB 820:REM " JSR $FFC0;OPEN"
7080 RETURN
7090 :
7100 REM -----------------------------------------
7110 REM"  {SHIFT-C}{SHIFT-L}{SHIFT-O}{SHIFT-S}{SHIFT-E}"
7120 REM -----------------------------------------
7130 :
7140 A$=CHR$($20)+CHR$($CC)+CHR$($FF):GOSUB 820:REM" JSR $FFCC ;CLRCHN"
7150 GOSUB 2320
7160 A$=CHR$($20)+CHR$($C3)+CHR$($FF):GOSUB 820:REM" JSR $FFC3 ;CLOSE"
7170 RETURN
7180 :
7190 REM -----------------------------------------
7200 REM"  {SHIFT-G}{SHIFT-E}{SHIFT-T}#"
7210 REM -----------------------------------------
7220 :
7230 B=P828(U):IF B<>35 THEN GOTO 7280:REM '#'
7240 U=U+1:GOSUB 2320:
7250 A$=CHR$($AA):GOSUB 820:REM" TAX"
7260 A$=CHR$($20)+CHR$($C6)+CHR$($FF):GOSUB 820:REM JSR $FFC6 ;CHKIN
7270 U=U+1
7280 A$=CHR$($20)+CHR$($E4)+CHR$($FF):GOSUB 820:REM" JSR $FFE4 ;GETIN"
7290 GOSUB 3110:IF V=0 THEN GOTO 2050
7300 A$=CHR$($A2)+CHR$(0)+CHR$($8D)+CHR$(L)+CHR$(H):GOSUB 820:REM " LDX #0: STA I"+V$""
7310 D=D+1:GOSUB 3810
7320 A$=CHR$($8E)+CHR$(L)+CHR$(H):GOSUB 820:REM STX I"+V$+"+1
7330 IF B=35 THEN A$=CHR$($20)+CHR$($CC)+CHR$($FF):GOSUB 820:REM" JSR $FFCC ;CLRCHN"
7340 RETURN
7350 :
7360 REM -----------------------------------------
7370 REM"  {SHIFT-P}{SHIFT-R}{SHIFT-I}{SHIFT-N}{SHIFT-T}#"
7380 REM -----------------------------------------
7390 :
7400 GOSUB 2320
7410 A$=CHR$($AA):GOSUB 820::REM" TAX"
7420 A$=CHR$($20)+CHR$($C9)+CHR$($FF):GOSUB 820:REM "JSR $FFC9 ;CHKOUT"
7430 U=U+1
7440 GOSUB 5310
7450 A$=CHR$($20)+CHR$($CC)+CHR$($FF):GOSUB 820:REM" JSR $FFCC ;CLRCHN"
7460 RETURN
7470 :
7480 REM
7490 REM ----------
7500 REM {SHIFT-E}{SHIFT-O}{SHIFT-F}
7510 REM ----------
